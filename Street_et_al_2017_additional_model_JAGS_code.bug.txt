### Model 

model{

  for (i in 1:N) {
    
    p.final[i] ~ dbeta(a_final[i], b_final[i]) # final ratings are modelled as a beta distribution

    a_final[i] <- final_pred[i] * phi_final # alpha shape parameter for the beta distribution

    b_final[i] <- (1 - final_pred[i]) * phi_final # beta shape parameter for the beta distribution

    logit(final_pred[i]) <- final_lp[i] # logit link function

    final_lp[i] <- logit(p.initial[i]) + social_influence[i] * social_deviation[i] # final ratings are predicted by the amount of social influence, relative to the amount that initial ratings and social information differ
    
    social_deviation[i] <- logit(p.social[i]) - logit(p.initial[i]) + 0.00001 # social deviation is the differences between the social information and the initial ratings, with a tiny constant added to avoid zero values
    
    social_influence[i] <- condition_influence_baseline[condition[i]] + random_player_influence_effect[condition[i], player[i]] # social influence can vary by image condition (faces, hands or artwork), and by a random effect of participant identity. Additionally, the random participant identity effect can differ by condition. 

  }

### Priors  

  phi_final ~ dexp(1) # exponential prior for the shape parameters for the beta distribution of modelled final ratings
  
  for (i in 1:3) { 
    condition_influence_baseline[i] ~ dnorm(0,1) # normal prior for the effect of image condition 
  }

  for (i in 1:3) {
  tau_players_social[i] ~ dexp(1) # exponential prior for the variance of the random participant effect
    for (j in 1:N_players) { 
      random_player_influence_effect[i, j] ~ dnorm(0, tau_players_social[i]) # normal prior for the random participant effect dependent on image condition (faces, hands or art). 
    }
  }
}

